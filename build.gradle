plugins {
    id 'fabric-loom' version '0.2.2-SNAPSHOT'
    id "com.matthewprenger.cursegradle" version "1.2.0"
    id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

group = "com.github.NerdHubMC"
archivesBaseName = "TextileLib"
version = System.getenv("TRAVIS_TAG") ?: project.mod_version

apply from: "https://raw.githubusercontent.com/NerdHubMC/Gradle-Scripts/master/scripts/fabric/basic_project.gradle"

minecraft {
}

//curseforge relations, used by the cf publishing script
if(project.hasProperty("curseforge_id")) {
    curseforge.curseProjects.each {
        if(it.id == project.findProject("curseforge_id")) {
            it.relations {
                requiredDependency "fabric"
            }
        }
    }
}

processResources {
    // this will ensure that this task is re-run when there's a change
    inputs.property "version", project.version

    // replace stuff in fabric.mod.json, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'fabric.mod.json'

        // add mod metadata
        expand 'version': project.version

        filter { String line ->
            line.replace("\"fabric\": \"*\"", "\"fabric\": \"[${project.fabric_version},)\"")
        }
    }

    // copy everything else, thats not the fabric.mod.json
    from(sourceSets.main.resources.srcDirs) {
        exclude 'fabric.mod.json'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}"
    modCompile "net.fabricmc:fabric-loader:${loader_version}"

    modCompile "net.fabricmc:fabric:${fabric_version}"
    compileOnly "com.google.code.findbugs:jsr305:${findbugs_version}"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = "TextileLib"
                url = "https://github.com/NerdHubMC/TextileLib"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/NerdHubMC/TextileLib/blob/master/LICENSE.md"
                    }
                }
                scm {
                    url = "https://github.com/NerdHubMC/TextileLib"
                    connection = "scm:git:git://github.com/NerdHubMC/TextileLib.git"
                    developerConnection = "scm:git:git@github.com:NerdHubMC/TextileLib.git"
                }
                issueManagement {
                    system = "github"
                    url = "https://github.com/NerdHubMC/TextileLib/issues"
                }
            }
        }
    }
}
